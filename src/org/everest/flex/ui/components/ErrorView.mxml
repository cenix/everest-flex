<?xml version="1.0" encoding="utf-8"?>
<!-- asdoc\ErrorView.mxml -->
<!---
A dialog to display an error message to the user. For convenience the static
method 'show' can be used to invoke this view.
-->
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:mx="library://ns.adobe.com/flex/mx"
               close="close();"
               width="500" height="280" title="Error"
               skinClass="org.everest.flex.ui.skins.ErrorViewSkin">
    <s:layout>
        <s:VerticalLayout paddingLeft="10" paddingTop="10" paddingRight="10" paddingBottom="10" />
    </s:layout>
    <fx:Script>
        <![CDATA[
            import mx.core.FlexGlobals;
            import mx.core.IFlexDisplayObject;
            import mx.managers.CursorManager;
            import mx.managers.PopUpManager;

            import org.as3commons.lang.StringUtils;

            private var _message:String;

            /**
             * Static convenience function to display this error popup dialog.
             *
             * @param errorMessage the error message to be shown
             */
            public static function show(errorMessage:String):void{
                var parent:DisplayObject = Sprite(FlexGlobals.topLevelApplication);
                var  p:IFlexDisplayObject = PopUpManager.createPopUp(parent, ErrorView, true);
                errorMessage.replace("<br>","\n");
                ErrorView(p).message = errorMessage;
                PopUpManager.centerPopUp(p);
                parent.addEventListener(Event.REMOVED_FROM_STAGE, function(evnent:Event):void{PopUpManager.removePopUp(p)});
                CursorManager.removeBusyCursor();
            }

            [Bindable]
            public function get message():String
            {
                return _message;
            }

            public function set message(value:String):void
            {
                if (value != null)
                {
                    value = value.replace( /--/g,"\n\n");

                    //see ticket 5838:
                    if (value.indexOf("otherwise incorrect.") > 0)
                    {
                        value = StringUtils.substringAfter(value, "otherwise incorrect.");

                    }else if (value.indexOf("requested operation.") > 0){
                        value = StringUtils.substringAfter(value, "requested operation.");
                    }
                    value = StringUtils.replace(value, "WSGI Server", "");
                }
                _message = value;
            }


            //Handle the close button and Cancel button.
            public function close():void {
                PopUpManager.removePopUp(this);
            }

            protected function buttonTrac_clickHandler(event:MouseEvent):void
            {
                navigateToURL( new URLRequest("http://trac/cenix/newticket?component=LOUICe&description=" + message + "&summary=LOUICe Error "));
            }

            protected function buttonChat_clickHandler(event:MouseEvent):void
            {
                navigateToURL( new URLRequest("ichat://gathmann@jabber.cenix-bioscience.com"));
            }
        ]]>
    </fx:Script>
    <s:Label  text="An Error has occured:" fontWeight="bold"/>
    <s:Scroller width="100%" height="100%">
        <s:RichEditableText width="100%" height="100%" minHeight="80" backgroundColor="#F4F4F4" editable="false"
                            paddingTop="5" paddingRight="5" paddingBottom="5" paddingLeft="5"
                            text="{message}"
                            verticalAlign="top"/>
    </s:Scroller>
    <s:HGroup>
        <s:Button label="Close" click="close()"/>
        <s:Button label="New Trac Ticket" click="buttonTrac_clickHandler(event)" id="buttonTrac"/>
    </s:HGroup>
</s:TitleWindow>