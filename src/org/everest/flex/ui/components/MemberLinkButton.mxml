<?xml version="1.0" encoding="utf-8"?>
<!-- asdoc\MemberLinkButton.mxml -->
<!---
A simple button that wraps around a INavigationLink object. When it is clicked
the link will be loaded by the everest flex framework.
-->
<s:Button xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:mx="library://ns.adobe.com/flex/mx"
               skinClass="org.everest.flex.ui.skins.LinkButtonSkin"
               click="linkClickHandler()" creationComplete="creationCompleteHandler(event)">
    <fx:Script>
        <![CDATA[
            import flash.desktop.Clipboard;
            import flash.desktop.ClipboardFormats;

            import mx.events.FlexEvent;

            import org.everest.flex.events.NavigationEvent;
            import org.everest.flex.interfaces.INavigationLink;
            import org.everest.flex.ui.presenters.MemberPresentationModel;

            [Bindable]
            private var _link:INavigationLink;

            public function get link():INavigationLink
            {
                return _link;
            }

            public function set link(value:INavigationLink):void
            {
                _link = value;
                label = _link.title;
            }

            protected function linkClickHandler():void
            {
                if (_link.href != null) {
                    var event:NavigationEvent = new NavigationEvent(NavigationEvent.LOAD_PAGE);
                    event.pageUrl = _link.href;
                    dispatchEvent(event);
                }
            }

            protected function creationCompleteHandler(event:FlexEvent):void
            {
                contextMenu = createContextMenu();
            }


            protected function openNewWindow(event:Event):void
            {
                event.preventDefault();

                var e:NavigationEvent = new NavigationEvent(NavigationEvent.NEW_WINDOW);
                    e.pageUrl = _link.href;
                dispatchEvent(e);
            }

            protected function createContextMenu():ContextMenu
            {
                var newWindow:ContextMenuItem = new ContextMenuItem("Open Link in New Tab", false);
                newWindow.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, openNewWindow);

                var cm:ContextMenu = new ContextMenu();
                cm.hideBuiltInItems();
                cm.customItems = [newWindow];

                return cm;
            }

        ]]>
    </fx:Script>
</s:Button>